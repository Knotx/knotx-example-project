// Maven Central release configuration


apply plugin: 'maven-publish'
apply plugin: 'signing'

task sourcesJar(type: Jar) {
  from sourceSets.main.allSource
  classifier = 'sources'
}

task javadocJar(type: Jar) {
  from javadoc
  classifier = 'javadoc'
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
      artifact sourcesJar
      artifact javadocJar

      pom {
        name = 'Knot.x Example Project'
        description = 'Knot.x Example Project'
        url = 'http://knotx.io/'
        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
        developers {
          developer {
            id = 'marcinczeczko'
            name = 'Marcin Czeczko'
            url = 'https://github.com/marcinczeczko'
          }
          developer {
            id = 'skejven'
            name = 'Maciej Laskowski'
            url = 'https://github.com/Skejven'
          }
          developer {
            id = 'tomaszmichalak'
            name = 'Tomasz Michalak'
            url = 'https://github.com/tomaszmichalak'
          }
        }
        scm {
          connection = 'scm:git:git://github.com/Knotx/knotx-example-project.git'
          developerConnection = 'scm:git:ssh://github.com:Knotx/knotx-example-project.git'
          url = 'http://github.com/Knotx/knotx-forms/tree/master'
        }
      }
    }
  }
  repositories {
    maven {
      def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
      def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
      url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
      credentials {
        username project.findProperty('ossrhUsername') ? "${ossrhUsername}" : "NOT_CONFIGURED"
        password project.findProperty('ossrhPassword') ? "${ossrhPassword}" : "NOT_CONFIGURED"

        println "Connecting with user: ${username}"
      }
    }
  }
}

signing {
  sign publishing.publications.mavenJava
}


javadoc {
  if (JavaVersion.current().isJava9Compatible()) {
    options.addBooleanOption('html4', true)
  }
}
