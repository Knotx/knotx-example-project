plugins {
  id 'java'
}

description = 'Acme Project - Sample datasource adapter'

defaultTasks 'build'


dependencies {
  compileOnly "io.knotx:knotx-dependencies:$knotxVersion"

  compileOnly 'io.knotx:knotx-databridge-api'
  compileOnly 'io.vertx:vertx-codegen'
  compileOnly 'io.vertx:vertx-jdbc-client'
  compileOnly 'org.hsqldb:hsqldb:2.4.0'

  testCompile 'io.knotx:knotx-databridge-api'
  testCompile 'io.knotx:knotx-junit5'

}
sourceSets {
  main {
    java {
      srcDirs += 'src/main/generated'
    }
  }
  test {
    resources {
      srcDirs += 'src/test/resources'
    }
  }
}

clean.doFirst {
  file('src/main/generated').deleteDir()
}

task annotationProcessing(type: JavaCompile, group: 'build') { // codegen
  source = sourceSets.main.java
  classpath = configurations.compile + configurations.compileOnly
  destinationDir = project.file('src/main/generated')
  options.compilerArgs = [
      "-proc:only",
      "-processor", "io.vertx.codegen.CodeGenProcessor",
      "-Acodegen.output=${destinationDir.absolutePath}"
  ]
}

task annotationProcessingCleanup() {
  doLast {
    delete "${project.projectDir}/src/main/generated"
    println "Removing: ${project.projectDir}/src/main/generated"
  }
}

annotationProcessing.dependsOn annotationProcessingCleanup

compileJava.dependsOn annotationProcessing

test {
  useJUnitPlatform()
  include 'io/knotx/databridge/**'

  systemProperties = [
      'vertx.logger-delegate-factory-class-name': 'io.vertx.core.logging.SLF4JLogDelegateFactory'
  ]
}
