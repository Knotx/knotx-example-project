plugins {
  id 'java'
}

group = 'com.acme.adapter'
description = 'Acme Project - Action Adapter HTTP'

defaultTasks 'build'

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
  options.compilerArgs << '-Xlint:deprecation'
  options.incremental = true
}

dependencies {
  compileOnly "io.knotx:knotx-dependencies:$knotxVersion"

  compileOnly 'io.knotx:knotx-core'
  compileOnly 'io.vertx:vertx-core'
  compileOnly 'io.vertx:vertx-rx-java2'
  compileOnly 'io.knotx:knotx-forms-api'
  compileOnly 'io.knotx:knotx-forms-core'
  compileOnly 'io.vertx:vertx-web-client'
  compileOnly 'io.vertx:vertx-service-proxy'
  compileOnly 'io.vertx:vertx-codegen'

  testCompile 'junit:junit'
  testCompile 'org.powermock:powermock-api-mockito'
  testCompile 'org.powermock:powermock-module-junit4'
  testCompile 'org.hamcrest:hamcrest-all'
  testCompile 'org.mockito:mockito-all'
  testCompile 'io.vertx:vertx-unit'
  testCompile 'io.vertx:vertx-config'
  testCompileOnly 'io.vertx:vertx-codegen'

  testCompile 'io.knotx:knotx-core::tests'
  testCompile "io.knotx:knotx-mocks:$knotxVersion"
}

sourceSets {
  main {
    java {
      srcDirs += 'src/main/generated'
    }
  }
  test {
    resources {
      srcDirs += 'src/test/resources'
    }
  }
}

clean.doFirst {
  file('src/main/generated').deleteDir()
}

task annotationProcessing(type: JavaCompile, group: 'build') { // codegen
  source = sourceSets.main.java
  classpath = configurations.compile + configurations.compileOnly
  destinationDir = project.file('src/main/generated')
  options.compilerArgs = [
      "-proc:only",
      "-processor", "io.vertx.codegen.CodeGenProcessor",
      "-Acodegen.output=${destinationDir.absolutePath}"
  ]
}

task annotationProcessingCleanup() {
  doLast {
    delete "${project.projectDir}/src/main/generated"
    println "Removing: ${project.projectDir}/src/main/generated"
  }
}

annotationProcessing.dependsOn annotationProcessingCleanup

compileJava.dependsOn annotationProcessing
